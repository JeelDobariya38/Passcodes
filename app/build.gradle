plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdk 33
    namespace "com.jeeldobariya.passcodes"

    defaultConfig {
        applicationId "com.jeeldobariya.passcodes"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "0.1.0-Alpha"
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    
    lintOptions {
        // You can enable lint checking and provide a baseline file if needed:
        // baseline file("lint-baseline.xml")

        // To ensure that lint uses the lint.xml configuration
        lintConfig rootProject.file("lint.xml")
    }

    buildTypes {
        release {
            if (rootProject.file("keystore.properties").exists()) {
                signingConfig signingConfigs.release
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                appIcon: "@mipmap/ic_launcher",
                appLabel: "@string/app_name"
            ]
        }

        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-Dev"
            minifyEnabled false
            
            // the name come from a parent project name "PassCodes"
            manifestPlaceholders = [
                appIcon: "@mipmap/dev_ic_launcher",
                appLabel: "Passcodes Dev"
            ]
        }

        staging {
            applicationIdSuffix ".staging"
            versionNameSuffix "-Staging"

            minifyEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                appIcon: "@mipmap/dev_ic_launcher",
                appLabel: "Passcodes Stageing"
            ]
        }
    }

    // You can update these to Java 11 or higher if your project allows,
    // but 1.8 is fine for basic Kotlin interop.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Add kotlinOptions for JVM target if you have specific requirements,
    // though often not strictly necessary for simple cases as it defaults
    // to Java 8 compatible bytecode.
    kotlinOptions {
        jvmTarget = '1.8' // Ensure Kotlin compiles to Java 8 bytecode
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.0"

    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.databinding:viewbinding:7.4.1'
    
    implementation 'androidx.room:room-ktx:2.5.0'
    kapt 'androidx.room:room-compiler:2.5.0'

    implementation 'org.json:json:20250517'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.4.4"
}
